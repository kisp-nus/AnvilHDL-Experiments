
import "aes_pkg.anvilh"
import "aes_key_expand.anvil"
import "aes_shift_rows.anvil"
import "aes_mix_columns.anvil"
import "aes_sub_bytes.anvil"

proc aes_cipher_core(ep : left aes_core_ch, entr_ep : right entropy_ch<128>){
    chan key_le -- key_ri : aes_key_expand_ch;
    chan shift_rows_le -- shift_rows_ri : shift_rows_ch;
    chan mix_col_le -- mix_col_ri : mix_column_ch;
    chan key_mix_le -- key_mix_ri : mix_column_ch;
    chan sub_bytes_le -- sub_bytes_ri : sub_bytes_ch;
    spawn aes_key_expand(key_le);
    spawn aes_shift_rows(shift_rows_le);
    spawn aes_mix_columns(mix_col_le);
    spawn aes_sub_bytes(sub_bytes_le);
    spawn aes_mix_columns(key_mix_le);

    reg cipher_state : logic[4][4][8];
    reg key : logic[8][32];
    reg dec_key : logic[8][32];
    reg cycle_count : logic[32];

    loop{
        set cycle_count := *cycle_count + 32'd1
    }

    loop{
            try key_gen = recv ep.dec_key_gen_req  {
                let key_init = recv ep.key_pack >>
                let key_len = recv ep.key_len_i >>
                send key_ri.init(init_pack::{op = ciph_op_e::CIPH_FWD; key_len = key_len}) >>
                set key := key_init >>

                generate_seq (i : 0, 9, 1){
                    send key_ri.req(aes_key_expand_in_pack::{key = *key; round = i; ctrl = init_pack::{op = ciph_op_e::CIPH_FWD; key_len = key_len}}) >>
                    let key_out_pack = recv key_ri.res >>
                    set key := key_out_pack.key
                } >>

                set dec_key := *key >>
                
                send ep.res(aes_core_out_pack::{state_o = *cipher_state;
                                                    // data_in_mask_o = #{ #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0}};
                                                    // alert_o = 1'd0; //To:Do fix this
                                                    // data_out_clear_o = 1'd0;
                                                    // key_clear_o = 1'd0;
                                                    // prng_reseed_o = 1'd0;
                                                    crypt_o = sp2v_e::SP2V_LOW;
                                                    dec_key_gen_o = sp2v_e::SP2V_HIGH})

            }   
            else {
                try data_in = recv ep.crypt{
                    let key_init_in = recv ep.key_pack >>
                    let key_len = recv ep.key_len_i >>
                    let ctrl_in = recv ep.ctrl >>

                    send key_ri.init(init_pack::{op = ctrl_in.op_i; key_len = key_len}) >>

                    // -------------Cycle 0 (Initialization)----------------
                    set cipher_state := data_in;
                    let key_init = match ctrl_in.op_i {
                        ciph_op_e::CIPH_FWD => key_init_in,
                        _ => *dec_key
                    };
                    set key := key_init >>
                    
                    // dprint "[Cycle %d] Initialization" (*cycle_count);


                    //----------------- Cycle 1 Round 0----------------
                    send key_ri.req(aes_key_expand_in_pack::{key = *key; round = 4'd0; ctrl = init_pack::{op = ctrl_in.op_i; key_len = key_len}}) >>
                    let key_out_pack = recv key_ri.res >> 
                    
                    let key_word = *key[0+:4];
                    let key_bytes_normal = call transpose(key_word);
                    let key_bytes = match ctrl_in.op_i{
                        ciph_op_e::CIPH_FWD => key_bytes_normal,
                        _ => (
                            send key_mix_ri.req(data_in_pack::{data = key_bytes_normal; op = ciph_op_e::CIPH_INV}) >>
                            let key_bytes = recv key_mix_ri.res >>
                            key_bytes
                        )
                    };
                    set cipher_state := *cipher_state ^ key_bytes;
                    set key := key_out_pack.key >> // do checks on error of the out pack
                    // dprint "[Cycle %d] Round 0" (*cycle_count);

                    // -------------Cycle 2-10 (Rounds)----------------
                    generate_seq (i : 1, 9,1){
                        // Key Gen
                        send key_ri.req(aes_key_expand_in_pack::{key = *key; round = i; ctrl = init_pack::{op = ctrl_in.op_i; key_len = key_len}}) >>
                        let key_out_pack = recv key_ri.res >>
                        let key_out = key_out_pack.key; // do checks on error of the out pack

                        // Sub Bytes
                        send sub_bytes_ri.req(data_in_pack::{data = *cipher_state; op = ctrl_in.op_i}) >>
                        let sub_bytes_out = recv sub_bytes_ri.res >>
                        
                        // Shift Rows
                        send shift_rows_ri.req(data_in_pack::{data = sub_bytes_out; op = ctrl_in.op_i}) >>
                        let shift_rows_out = recv shift_rows_ri.res >>
            
                        // Mix Columns
                        send mix_col_ri.req(data_in_pack::{data = shift_rows_out; op = ctrl_in.op_i}) >>
                        let mix_col_out = recv mix_col_ri.res >>
                        
                        // Add Round Key
                        let key_word= *key[0+:4];
                        let key_bytes_normal = call transpose(key_word);
                        let key_bytes = match ctrl_in.op_i{
                            ciph_op_e::CIPH_FWD => key_bytes_normal ,
                            _ => (
                                send key_mix_ri.req(data_in_pack::{data = key_bytes_normal; op = ciph_op_e::CIPH_INV}) >>
                                let key_bytes = recv key_mix_ri.res >>
                                key_bytes
                            )
                    
                        };
            
                        let add_rk_out = mix_col_out^key_bytes;
                        set cipher_state := add_rk_out;
                        set key := key_out
                        // dprint "[Cycle %d] Round %d" (*cycle_count, i)
                    }>>

                    // -------------Cycle 11 (Final Round)----------------

                    //Sub Bytes
                    send sub_bytes_ri.req(data_in_pack::{data = *cipher_state; op = ctrl_in.op_i}) >>
                    let sub_bytes_out = recv sub_bytes_ri.res >>


                    //Shift Rows
                    send shift_rows_ri.req(data_in_pack::{data = sub_bytes_out; op = ctrl_in.op_i}) >>
                    let shift_rows_out = recv shift_rows_ri.res >>

                    //Add Round Key
                    let key_word = *key[0+:4];
                    let key_bytes_normal = call transpose(key_word);
                    let key_bytes = match ctrl_in.op_i{
                        ciph_op_e::CIPH_FWD => key_bytes_normal,
                        _ => (
                            send key_mix_ri.req(data_in_pack::{data = key_bytes_normal; op = ciph_op_e::CIPH_INV}) >>
                            let key_bytes = recv key_mix_ri.res >>
                            key_bytes
                        )
                    };
                    set cipher_state := shift_rows_out^key_bytes>>
                    // dprint "[Cycle %d] Final Round" (*cycle_count);

                    // -------------Cycle 12 (Output)----------------
                    send ep.res(aes_core_out_pack::{state_o = *cipher_state;
                                                    // data_in_mask_o = #{ #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0},
                                                    //                     #{8'd0, 8'd0,8'd0, 8'd0}};
                                                    // alert_o = 1'd0; //To:Do fix this
                                                    // data_out_clear_o = ctrl_in.data_out_clear_i;
                                                    // key_clear_o = ctrl_in.key_clear_i;
                                                    // prng_reseed_o = ctrl_in.prng_reseed_i;
                                                    dec_key_gen_o = sp2v_e::SP2V_LOW;
                                                    crypt_o = sp2v_e::SP2V_HIGH}) >>
                    cycle 1
                }
                else {
                    cycle 1
                }
            }

    }

    loop {
        send entr_ep.req(1'd1) >>
        let entropy = recv entr_ep.res >>
        cycle 1
    }

}